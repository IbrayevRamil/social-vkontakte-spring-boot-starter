buildscript {
  repositories {
    maven { url 'http://repo.spring.io/plugins-release' }
    jcenter()
  }

  dependencies {
    classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.5'
    classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0'
    classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
  }
}

def rootProject=project

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: 'propdeps'

ext {
  project_website = "https://github.com/${developerId}/${projectId}"
  project_scm = "scm:git@github.com:${developerId}/${projectId}.git"
  license_url = "https://raw.github.com/${developerId}/${projectId}/master/LICENSE"
}

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories {
  jcenter()
  maven { url 'http://fugru.com/archiva/repository/snapshots' }
}

dependencies {
  compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
  compile("org.springframework.social:spring-social-config:$springSocialVersion")
  compile("org.springframework.social:spring-social-core:$springSocialVersion")
  compile("org.springframework.social:spring-social-web:$springSocialVersion")
  compile("org.springframework.social:spring-social-vkontakte:$springSocialVkontakteVersion")
  optional("org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}")
}

compileJava.dependsOn(processResources)

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates sources jar') {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, description: 'Creates javadoc jar') {
  dependsOn 'javadoc'
}

artifacts {
  archives sourcesJar, javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom.withXml {
        asNode().with {
//          appendNode('name', rootProject.name)
//          appendNode('packaging', 'jar')
          appendNode('description', rootProject.description)
          appendNode('url', rootProject.project_website)
          appendNode('scm').with {
            appendNode('url', rootProject.project_scm)
            appendNode('connection', rootProject.project_scm)
            appendNode('developerConnection', rootProject.project_scm)
          }
          appendNode('licenses').with {
            appendNode('license').with {
              appendNode('name', rootProject.license)
              appendNode('url', rootProject.license_url)
              appendNode('distribution', 'repo')
            }
          }
          appendNode('developers').with {
            appendNode('developer').with {
              appendNode('id', rootProject.developerId)
              appendNode('name', rootProject.developerName)
            }
          }
          it
        }
      }

      from components.java
//
      artifact sourcesJar {
        classifier 'sources'
      }

      artifact javadocJar {
        classifier "javadoc"
      }
    }
  }
}

project.tasks.build.finalizedBy project.tasks.publishToMavenLocal

if(file('local.gradle').exists()) {
  apply from: 'local.gradle'
}
// oss.jfrog.org artifactory repository
def bintrayUser = project.hasProperty('bintrayUser') ? project.bintrayUser : ''
def bintrayKey = project.hasProperty('bintrayKey') ? project.bintrayKey : ''

println "bintrayUser: $bintrayUser, bintrayKey: $bintrayKey"

artifactory {
  contextUrl = 'https://oss.jfrog.org/artifactory'
  publish {
    repository {
      if (project.version.endsWith('-SNAPSHOT'))
        repoKey = 'oss-snapshot-local'
      else
        repoKey = 'oss-release-local'
      username = bintrayUser
      password = bintrayKey
    }
    defaults {
      publications 'mavenJava'
    }
  }
  resolve {
    repository {
      repoKey = 'repo'
    }
  }
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}

